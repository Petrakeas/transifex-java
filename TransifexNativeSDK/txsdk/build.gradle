plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    namespace = 'com.transifex.txnative'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Have aapt2 use src/test/res for resource generation. Unfortunately, the test strings
    // end up in the main source set and the resulting .aar
    sourceSets {
        main {
            res.srcDirs += ['src/test/res']
        }
    }

    // https://developer.android.com/studio/publish-library/configure-pub-variants
    publishing {
        singleVariant('release') {
            // Currently, Javadoc generation from AGP does not have the same quality as the one
            // generated by the script at publish-helper.gradle so it's commented out.
//            withJavadocJar()
            withSourcesJar()
        }
    }
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

configurations.all  {
    resolutionStrategy {
        force "androidx.annotation:annotation:$versions.androidXAnnotation"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    compileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    compileOnly 'androidx.appcompat:appcompat:1.5.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation 'io.github.inflationx:viewpump:2.0.3'
    api project(':common')

    testCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation testFixtures(project(':common'))

    androidTestCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1'
    androidTestImplementation testFixtures(project(':common'))
}

apply from: '../publish-helper.gradle'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Specify custom artifactId if needed,
                // otherwise it would use module's name by default.
                artifactId = "txsdk"
            }
        }
    }
}