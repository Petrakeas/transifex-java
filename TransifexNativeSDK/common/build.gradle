plugins {
    id 'java'
    id 'java-test-fixtures'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
compileTestFixturesJava.options.encoding = "UTF-8"
javadoc.options.encoding = 'UTF-8'

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

// Disable publishing of test fixtures variants
// https://docs.gradle.org/current/userguide/java_testing.html#sec:java_test_fixtures
components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

dependencies {
    compileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'net.moznion:uribuilder-tiny:2.7.1'

    testCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testImplementation 'commons-io:commons-io:2.6'

    testFixturesCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    testFixturesImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testFixturesImplementation 'com.google.code.gson:gson:2.8.9'
}

// Generate a BuildProperties Java class
task generateJava {
    ext.outputDir = "$buildDir/generated/java"
    doLast {
        mkdir "$outputDir/com/transifex/common"
        file("$outputDir/com/transifex/common/BuildProperties.java").text =
                """|package com.transifex.common;
               |public class BuildProperties {
               |    public static String getSDKVersion() { return "${rootProject.ext.sdkVersion}"; }
               |    public static String getCLIVersion() { return "${rootProject.ext.cliVersion}"; }
               |}""".stripMargin()
    }
}
compileJava.dependsOn generateJava
sourceSets.main.java.srcDir generateJava.outputDir

apply from: '../publish-helper.gradle'